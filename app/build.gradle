plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'de.mannodermaus.android-junit5'
}

android {
    compileSdkVersion rootProject.compile_sdk_version
    buildToolsVersion rootProject.build_tools_version

    defaultConfig {
        applicationId "org.emunix.insteadlauncher"
        minSdkVersion rootProject.min_sdk_version
        targetSdkVersion rootProject.compile_sdk_version
        versionCode rootProject.version_code
        versionName rootProject.version_name
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    ndkVersion rootProject.ndk_version
    signingConfigs {
        release
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "INSTEAD_VERSION", "\"$rootProject.build_config_instead_version\"")
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '-DEBUG'
            buildConfigField("String", "INSTEAD_VERSION", "\"$rootProject.build_config_instead_version\"")
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        viewBinding true
    }
    lintOptions {
        abortOnError false
    }
}

Properties props = new Properties()
def propFile = rootProject.file("keystore.properties")
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('storeFile') && props.containsKey('storePassword') &&
            props.containsKey('keyAlias') && props.containsKey('keyPassword')) {

        println 'Release build signing'

        android.signingConfigs.release.storeFile = file(props['storeFile'])
        android.signingConfigs.release.storePassword = props['storePassword']
        android.signingConfigs.release.keyAlias = props['keyAlias']
        android.signingConfigs.release.keyPassword = props['keyPassword']
    } else {
        println 'Release build not found signing properties'

        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'Release build not found signing file'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"

    implementation project(':core-storage')
    implementation project(':core-preferences')
    implementation project(':instead')

    // Support libraries
    implementation "androidx.appcompat:appcompat:$rootProject.appcompat_version"
    implementation 'com.google.android.material:material:1.4.0'
    implementation "androidx.fragment:fragment-ktx:1.3.6"
    implementation "androidx.preference:preference-ktx:$rootProject.preference_ktx_version"
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'

    // Architecture components
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$rootProject.lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$rootProject.lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$rootProject.lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"

    // Room
    implementation "androidx.room:room-runtime:$rootProject.room_version"
    kapt "androidx.room:room-compiler:$rootProject.room_version"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$rootProject.kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.kotlin_coroutines_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.navigation_version"

    // WorkManager
    implementation "androidx.work:work-runtime:2.6.0"

    // RxJava
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.4'

    // Network
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.picasso:picasso:2.71828'

    // DI
    implementation "com.google.dagger:hilt-android:$rootProject.hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$rootProject.hilt_version"
    implementation "androidx.hilt:hilt-work:1.0.0"
    kapt("androidx.hilt:hilt-compiler:1.0.0")

    // Files
    implementation "commons-io:commons-io:$rootProject.commons_io_version"

    // Crash reports
    implementation "ch.acra:acra-mail:$rootProject.acraVersion"
    implementation "ch.acra:acra-notification:$rootProject.acraVersion"

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // LeakCanary
    //debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.6'
}

repositories {
    mavenCentral()
}

