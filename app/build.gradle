apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.mannodermaus.android-junit5'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'
    defaultConfig {
        applicationId "org.emunix.insteadlauncher"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 802
        versionName "0.8.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_PLATFORM=android-21", "-DCMAKE_BUILD_TYPE=Release",
                        "-DANDROID_ARM_MODE=arm"
                version "3.10.2"
            }
        }
    }
    ndkVersion "20.1.5948944"
    signingConfigs {
        release
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "INSTEAD_VERSION", "\"3.3.2\"")
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '-DEBUG'
            buildConfigField("String", "INSTEAD_VERSION", "\"3.3.2\"")
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    lintOptions {
        abortOnError false
    }
}

Properties props = new Properties()
def propFile = rootProject.file("keystore.properties")
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('storeFile') && props.containsKey('storePassword') &&
            props.containsKey('keyAlias') && props.containsKey('keyPassword')) {

        println 'Release build signing'

        android.signingConfigs.release.storeFile = file(props['storeFile'])
        android.signingConfigs.release.storePassword = props['storePassword']
        android.signingConfigs.release.keyAlias = props['keyAlias']
        android.signingConfigs.release.keyPassword = props['keyPassword']
    } else {
        println 'Release build not found signing properties'

        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'Release build not found signing file'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"

    // Support libraries
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.10"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.0'

    // Architecture components
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.2.0"
    implementation "androidx.room:room-runtime:2.2.5"
    kapt "androidx.room:room-compiler:2.2.5"

    // WorkManager
    implementation "androidx.work:work-runtime:2.4.0"

    // RxJava
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.4'

    // Network
    implementation 'com.squareup.okhttp3:okhttp:4.2.2'
    implementation 'com.squareup.picasso:picasso:2.71828'

    // DI
    implementation 'com.google.dagger:dagger:2.28'
    kapt 'com.google.dagger:dagger-compiler:2.28'

    // Files
    implementation 'commons-io:commons-io:2.5' // 2.6 crashes the application on Android below 8, because java.nio.file is not available in these versions

    // LeakCanary
    //debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-3'
}

repositories {
    mavenCentral()
}

task deleteDependencies(type: Delete) {
    delete 'src/main/cpp/Instead/Instead', 'src/main/cpp/Lua/Lua'
    delete 'src/main/cpp/SDL2/SDL2', 'src/main/cpp/SDL2_image/SDL2_image'
    delete 'src/main/cpp/SDL2_mixer/SDL2_mixer', 'src/main/cpp/SDL2_ttf/SDL2_ttf'
    delete 'src/main/cpp/libiconv/libiconv'
}

task downloadDependencies(dependsOn: deleteDependencies) {
    def Instead = '3.3.2'
    def Lua = '5.1.5'
    def SDL2 = '2.0.12'
    def SDL2_image = '2.0.5'
    def SDL2_mixer = '2.0.4'
    def SDL2_ttf = '2.0.15'
    def Iconv = '1.15'

    doLast {
        download {
            src "https://github.com/instead-hub/instead/archive/${Instead}.tar.gz"
            dest new File(buildDir, "instead-${Instead}.tar.gz")
        }
        copy {
            from tarTree(resources.gzip("${buildDir}/instead-${Instead}.tar.gz"))
            into 'src/main/cpp/Instead/'
        }
        file("src/main/cpp/Instead/instead-${Instead}").renameTo(file('src/main/cpp/Instead/Instead'))
        delete("src/main/cpp/Instead/instead-${Instead}")
        delete("src/main/cpp/Instead/pax_global_header")

        download {
            src "https://www.lua.org/ftp/lua-${Lua}.tar.gz"
            dest buildDir
        }
        copy {
            from tarTree(resources.gzip("${buildDir}/lua-${Lua}.tar.gz"))
            into 'src/main/cpp/Lua/'
        }
        file("src/main/cpp/Lua/lua-${Lua}").renameTo(file('src/main/cpp/Lua/Lua'))
        delete("src/main/cpp/Lua/lua-${Lua}")

        download {
            src "https://www.libsdl.org/release/SDL2-${SDL2}.tar.gz"
            dest buildDir
        }
        copy {
            from tarTree(resources.gzip("${buildDir}/SDL2-${SDL2}.tar.gz"))
            into 'src/main/cpp/SDL2/'
        }
        file("src/main/cpp/SDL2/SDL2-${SDL2}").renameTo(file('src/main/cpp/SDL2/SDL2'))
        delete("src/main/cpp/SDL2/SDL2-${SDL2}")

        download {
            src "https://www.libsdl.org/projects/SDL_image/release/SDL2_image-${SDL2_image}.tar.gz"
            dest buildDir
        }
        copy {
            from tarTree(resources.gzip("${buildDir}/SDL2_image-${SDL2_image}.tar.gz"))
            into 'src/main/cpp/SDL2_image/'
        }
        file("src/main/cpp/SDL2_image/SDL2_image-${SDL2_image}").renameTo(file('src/main/cpp/SDL2_image/SDL2_image'))
        delete("src/main/cpp/SDL2_image/SDL2_image-${SDL2_image}")

        download {
            src "https://www.libsdl.org/projects/SDL_mixer/release/SDL2_mixer-${SDL2_mixer}.tar.gz"
            dest buildDir
        }
        copy {
            from tarTree(resources.gzip("${buildDir}/SDL2_mixer-${SDL2_mixer}.tar.gz"))
            into 'src/main/cpp/SDL2_mixer/'
        }
        file("src/main/cpp/SDL2_mixer/SDL2_mixer-${SDL2_mixer}").renameTo(file('src/main/cpp/SDL2_mixer/SDL2_mixer'))
        delete("src/main/cpp/SDL2_mixer/SDL2_mixer-${SDL2_mixer}")

        download {
            src "https://www.libsdl.org/projects/SDL_ttf/release/SDL2_ttf-${SDL2_ttf}.tar.gz"
            dest buildDir
        }
        copy {
            from tarTree(resources.gzip("${buildDir}/SDL2_ttf-${SDL2_ttf}.tar.gz"))
            into 'src/main/cpp/SDL2_ttf/'
        }
        file("src/main/cpp/SDL2_ttf/SDL2_ttf-${SDL2_ttf}").renameTo(file('src/main/cpp/SDL2_ttf/SDL2_ttf'))
        delete("src/main/cpp/SDL2_ttf/SDL2_ttf-${SDL2_ttf}")

        download {
            src "https://ftp.gnu.org/pub/gnu/libiconv/libiconv-${Iconv}.tar.gz"
            dest buildDir
        }
        copy {
            from tarTree(resources.gzip("${buildDir}/libiconv-${Iconv}.tar.gz"))
            into 'src/main/cpp/libiconv/'
        }
        file("src/main/cpp/libiconv/libiconv-${Iconv}").renameTo(file('src/main/cpp/libiconv/libiconv'))
        delete("src/main/cpp/libiconv/libiconv-${Iconv}")
    }
}

task copyLangs(type: Copy) {
    description = 'Copies langs from Instead to assets'
    from 'src/main/cpp/Instead/Instead/lang/'
    into 'src/main/assets/lang/'
    include '*.ini'
}

task copyThemes(type: Copy) {
    description = 'Copies themes from Instead to assets'
    from 'src/main/cpp/Instead/Instead/themes/'
    into 'src/main/assets/themes/'
    exclude 'CMakeLists.txt', 'Makefile'
}

task copyStead(type: Copy) {
    description = 'Copies stead folder from Instead to assets'
    from 'src/main/cpp/Instead/Instead/stead/'
    into 'src/main/assets/stead/'
    exclude '**/CMakeLists.txt', 'Makefile'
}

task cleanAssets(type: Delete) {
    delete 'src/main/assets/lang/', 'src/main/assets/themes/', 'src/main/assets/stead/'
}

preBuild.dependsOn copyLangs, copyThemes, copyStead

afterEvaluate {
    if (project.hasProperty("packageRelease")) {
        packageRelease.finalizedBy(cleanAssets)
    }
    if (project.hasProperty("packageDebug")) {
        packageDebug.finalizedBy(cleanAssets)
    }
    if (project.hasProperty("clean")) {
        clean.finalizedBy(cleanAssets)
    }
}
