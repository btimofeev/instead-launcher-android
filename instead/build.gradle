plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id "de.undercouch.download" version "5.4.0"
}

android {

    defaultConfig {
        minSdkVersion rootProject.min_sdk_version
        compileSdk rootProject.compile_sdk_version
        targetSdkVersion rootProject.compile_sdk_version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_PLATFORM=android-21", "-DCMAKE_BUILD_TYPE=Release",
                        "-DANDROID_ARM_MODE=arm"
                version "3.22.1"
            }
        }
        lintOptions {
          abortOnError false
        }

        consumerProguardFiles "consumer-rules.pro"
    }

    ndkVersion ndk_version

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "INSTEAD_VERSION", "\"$rootProject.build_config_instead_version\"")
            ndk {
                debugSymbolLevel = 'FULL'
            }
        }
        debug {
            buildConfigField("String", "INSTEAD_VERSION", "\"$rootProject.build_config_instead_version\"")
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    externalNativeBuild {
        cmake {
            path "src/main/c/CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    namespace 'org.emunix.instead'
}

dependencies {
    implementation project(':core-storage')
    implementation project(':core-preferences')
    implementation project(':sdl-activity')

    implementation "androidx.appcompat:appcompat:$rootProject.appcompat_version"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$rootProject.kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.kotlin_coroutines_version"

    // DI
    implementation "com.google.dagger:hilt-android:$rootProject.hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$rootProject.hilt_version"
}

task deleteDependencies(type: Delete) {
    delete 'src/main/c/Instead/Instead', 'src/main/c/Lua/Lua'
    delete 'src/main/c/SDL2/SDL2', 'src/main/c/SDL2_image/SDL2_image'
    delete 'src/main/c/SDL2_mixer/SDL2_mixer', 'src/main/c/SDL2_ttf/SDL2_ttf'
    delete 'src/main/c/libiconv/libiconv'
}

task downloadDependencies(dependsOn: deleteDependencies) {
    def Instead = '3.5.0'
    def Lua = '5.1.5'
    def SDL2 = '2.0.22'
    def SDL2_image = '2.0.5'
    def SDL2_mixer = '2.0.4'
    def SDL2_ttf = '2.0.15'
    def Iconv = '1.15'

    doLast {
        download.run {
            src "https://github.com/instead-hub/instead/archive/${Instead}.tar.gz"
            dest new File(buildDir, "instead-${Instead}.tar.gz")
        }
        copy {
            from tarTree(resources.gzip("${buildDir}/instead-${Instead}.tar.gz"))
            into 'src/main/c/Instead/'
        }
        delete("${buildDir}/instead-${Instead}.tar.gz")
        file("src/main/c/Instead/instead-${Instead}").renameTo(file('src/main/c/Instead/Instead'))
        delete("src/main/c/Instead/instead-${Instead}")
        delete("src/main/c/Instead/pax_global_header")

        download.run {
            src "https://www.lua.org/ftp/lua-${Lua}.tar.gz"
            dest buildDir
        }
        copy {
            from tarTree(resources.gzip("${buildDir}/lua-${Lua}.tar.gz"))
            into 'src/main/c/Lua/'
        }
        delete("${buildDir}/lua-${Lua}.tar.gz")
        file("src/main/c/Lua/lua-${Lua}").renameTo(file('src/main/c/Lua/Lua'))
        delete("src/main/c/Lua/lua-${Lua}")

        download.run {
            src "https://www.libsdl.org/release/SDL2-${SDL2}.tar.gz"
            dest buildDir
        }
        copy {
            from tarTree(resources.gzip("${buildDir}/SDL2-${SDL2}.tar.gz"))
            into 'src/main/c/SDL2/'
        }
        delete("${buildDir}/SDL2-${SDL2}.tar.gz")
        file("src/main/c/SDL2/SDL2-${SDL2}").renameTo(file('src/main/c/SDL2/SDL2'))
        delete("src/main/c/SDL2/SDL2-${SDL2}")

        download.run {
            src "https://www.libsdl.org/projects/SDL_image/release/SDL2_image-${SDL2_image}.tar.gz"
            dest buildDir
        }
        copy {
            from tarTree(resources.gzip("${buildDir}/SDL2_image-${SDL2_image}.tar.gz"))
            into 'src/main/c/SDL2_image/'
        }
        delete("${buildDir}/SDL2_image-${SDL2_image}.tar.gz")
        file("src/main/c/SDL2_image/SDL2_image-${SDL2_image}").renameTo(file('src/main/c/SDL2_image/SDL2_image'))
        delete("src/main/c/SDL2_image/SDL2_image-${SDL2_image}")

        download.run {
            src "https://www.libsdl.org/projects/SDL_mixer/release/SDL2_mixer-${SDL2_mixer}.tar.gz"
            dest buildDir
        }
        copy {
            from tarTree(resources.gzip("${buildDir}/SDL2_mixer-${SDL2_mixer}.tar.gz"))
            into 'src/main/c/SDL2_mixer/'
        }
        delete("${buildDir}/SDL2_mixer-${SDL2_mixer}.tar.gz")
        file("src/main/c/SDL2_mixer/SDL2_mixer-${SDL2_mixer}").renameTo(file('src/main/c/SDL2_mixer/SDL2_mixer'))
        delete("src/main/c/SDL2_mixer/SDL2_mixer-${SDL2_mixer}")

        download.run {
            src "https://www.libsdl.org/projects/SDL_ttf/release/SDL2_ttf-${SDL2_ttf}.tar.gz"
            dest buildDir
        }
        copy {
            from tarTree(resources.gzip("${buildDir}/SDL2_ttf-${SDL2_ttf}.tar.gz"))
            into 'src/main/c/SDL2_ttf/'
        }
        delete("${buildDir}/SDL2_ttf-${SDL2_ttf}.tar.gz")
        file("src/main/c/SDL2_ttf/SDL2_ttf-${SDL2_ttf}").renameTo(file('src/main/c/SDL2_ttf/SDL2_ttf'))
        delete("src/main/c/SDL2_ttf/SDL2_ttf-${SDL2_ttf}")

        download.run {
            src "https://ftp.gnu.org/pub/gnu/libiconv/libiconv-${Iconv}.tar.gz"
            dest buildDir
        }
        copy {
            from tarTree(resources.gzip("${buildDir}/libiconv-${Iconv}.tar.gz"))
            into 'src/main/c/libiconv/'
        }
        delete("${buildDir}/libiconv-${Iconv}.tar.gz")
        file("src/main/c/libiconv/libiconv-${Iconv}").renameTo(file('src/main/c/libiconv/libiconv'))
        delete("src/main/c/libiconv/libiconv-${Iconv}")
    }
}

task copyLangs(type: Copy) {
    description = 'Copies langs from Instead to assets'
    from 'src/main/c/Instead/Instead/lang/'
    into 'src/main/assets/lang/'
    include '*.ini'
}

task copyThemes(type: Copy) {
    description = 'Copies themes from Instead to assets'
    from 'src/main/c/Instead/Instead/themes/'
    into 'src/main/assets/themes/'
    exclude 'CMakeLists.txt', 'Makefile'
}

task copyStead(type: Copy) {
    description = 'Copies stead folder from Instead to assets'
    from 'src/main/c/Instead/Instead/stead/'
    into 'src/main/assets/stead/'
    exclude '**/CMakeLists.txt', 'Makefile'
}

task cleanAssets(type: Delete) {
    delete 'src/main/assets/lang/', 'src/main/assets/themes/', 'src/main/assets/stead/'
}

task fdroidRemoveBinaries(type: Delete) {
    delete 'src/main/c/Instead/Instead/contrib/'
    delete 'src/main/c/SDL2/SDL2/Xcode/'
    delete 'src/main/c/SDL2_image/SDL2_image/Xcode/'
    delete 'src/main/c/SDL2_image/SDL2_image/external/libwebp-1.0.2/swig/'
    delete 'src/main/c/SDL2_mixer/SDL2_mixer/Xcode/'
    delete 'src/main/c/SDL2_ttf/SDL2_ttf/Xcode/'
    delete 'src/main/c/libiconv/libiconv/tests/'
}

preBuild.dependsOn copyLangs, copyThemes, copyStead

afterEvaluate {
    if (project.hasProperty("packageRelease")) {
        packageRelease.finalizedBy(cleanAssets)
    }
    if (project.hasProperty("packageDebug")) {
        packageDebug.finalizedBy(cleanAssets)
    }
    if (project.hasProperty("clean")) {
        clean.finalizedBy(cleanAssets)
    }
}